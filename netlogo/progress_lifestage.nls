
to progress-lifestage
  
  ;; First, increase the age of every person in the system
  set peopleList map [replace-item ageAcc ? ((item ageAcc ?) + 1)] peopleList
  
  ;; Also, increase number of years since moving
  set timeSinceMoving timeSinceMoving + 1
  
  if check-death [updateHouseholdAttributes]
  if reproduce [updateHouseholdAttributes]
  if move-out-children [updateHouseholdAttributes]
  if merge-household [updateHouseholdAttributes]
  if retire [updateHouseholdAttributes]
  
  ;; If there are no people left in household, remove turtle
  if empty? peopleList [ die  ]
end



to updateHouseholdAttributes
  let peopleIterator 0
  set NoOfpeopleInService 0
  set NoOfpeopleInIT 0 
  set NoOfpeopleInFinance 0
  set NoOfpeopleInNonProfit 0
  
  while [peopleIterator < length peopleList][
    if item jobAcc item peopleIterator peopleList = 5 AND item ageAcc item peopleIterator peopleList > 18[
      set NoOfpeopleInIT NoOfpeopleInIT + 1
    ]
    if item jobAcc item peopleIterator peopleList = 4 AND item ageAcc item peopleIterator peopleList > 18[
      set NoOfpeopleInFinance NoOfpeopleInFinance + 1
    ]
    if item jobAcc item peopleIterator peopleList = 3 AND item ageAcc item peopleIterator peopleList > 18[
      set NoOfpeopleInService NoOfpeopleInService + 1
    ]
    if item jobAcc item peopleIterator peopleList = 6 AND item ageAcc item peopleIterator peopleList > 18[
      set NoOfpeopleInNonProfit NoOfpeopleInNonProfit + 1
    ]
       
    set peopleIterator peopleIterator + 1
  ]      
  
end

to-report retire
  let changeFlag FALSE
  foreach peopleList [
    if item ageAcc ? > 65 [
      ;; This is a rather ugly hack to let people who retire move to less crowded cities. 
      ;; Instead of altering their regular attractiveness we change their job to agriculture
      ;; which are natrually more inclined to move to quieter cities or to out of city
      set ? replace-item jobAcc ? 1
      set changeFlag TRUE
    ]
  ]
  
  report changeflag
end

to-report check-death
  let changeFlag FALSE
  foreach peopleList [
    ;; Then, check if people in the household die
    ;; A random value is drawn from a normal distribution with u = 80 and s  = 6
    ;; which is then bounded between 60 and 100
    let randomDeathAge bounded-random-number (random-normal 80 6) 60 100 "true"
    if item ageAcc ? > randomDeathAge [  
      set peopleList remove ? peopleList
      set changeFlag TRUE
    ]
    
  ]
  report changeFlag
end

to-report reproduce
  let changeFlag FALSE
  ;; Make babies
  ;; Every two member household where both are aged < 40 have a chance to make babies
  ;; On average, households are in reproductive age for 14 years ->
  ;; On average, we need about 2 children per household
  ;; Therefore, average chance of baby per year is 1 in 7
  ;; first, check if two oldest members are < 40
  ;; ASSUMED: List of people is always sorted by age DESC
  if length peopleList >= 2 AND length peopleList <= 5 [
    if item ageAcc (item 0 peopleList) < 42 AND item ageAcc (item 1 peopleList) < 42 [
      let overPopulationModifier 2 * ((noOfHouseholds / count turtles) - 1)
      if random-float 7.0 <= 1 + overPopulationModifier [
        let child generate-member 0 1 "random" 
        set peopleList lput child peopleList
        set changeFlag TRUE
      ]
    ]    
  ]
  report changeFlag
end


to-report move-out-children
  let changeFlag FALSE
  ;; Let children move out of house when they are aged
  if length peopleList > 2 [
    ;; Loop over a list of all children in the household between 16 and 23
    foreach filter [item ageAcc ? >= 16 AND item ageAcc ? <= 23] peopleList [
      ;; First create a randomly distributed but bounded random number between 16 and 23 to determine when a child moves out.
      let randomMovingAge bounded-random-number (random-normal 19.5 2) 16 23 "true"
      
      ;; If the age of the child is the randomly drawn age or 23, move him out.
      if item ageAcc ? = randomMovingAge OR item ageAcc ? = 23  [
        ;; Remove the child from the current household and create a new turtle containing the single child.
        hatch 1[
          set peopleList (list ?)
          set timeSinceMoving 0
        ]      
        set peopleList remove ? peopleList 
        set changeFlag TRUE    
      ]
    ]
  ] 
  report changeFlag
end



to-report merge-household
  let changeFlag FALSE
  ;; Let households merge 
  if length peopleList = 1 [
    let personAge item ageAcc (item 0 peopleList) 
    if personAge < 50 [
      ;; First create a randomly distributed but bounded random number between 16 and 23 to determine when a child moves out.
      let randomMergingAge bounded-random-number (random-normal 26.5 2) 23 30 "true"
      
      ;; If the age of the person is the randomly drawn age or 30, let him merge with another random household of the other sex within x patches. 
      if personAge = randomMergingAge OR personAge > 30[
        let targetSex ""
        let targetPerson ""
        let targetLocation pcolor
        ifelse item sexAcc (item 0 peopleList) = "male" [ set targetSex "female" ][ set targetSex "male"]
        let target one-of turtles with [pcolor = targetLocation AND length peopleList = 1 AND item sexAcc (item 0 peopleList)  = targetSex AND item ageAcc (item 0 peopleList) < 50]
        if target != nobody[          
          Ask target[
            set targetPerson item 0 peopleList
            die
            set changeFlag TRUE
          ]
          set peopleList lput targetPerson peopleList
        ]
        
      ]    
    ]
  ]  
  report changeFlag
  
end
